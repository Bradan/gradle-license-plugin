apply plugin: "maven-publish"
apply plugin: "com.jfrog.artifactory"
apply plugin: "com.jfrog.bintray"

def pomConfig = {
  //noinspection UnnecessaryQualifiedReference
  resolveStrategy = Closure.DELEGATE_FIRST
  name project.name
  description project.description
  url "https://github.com/jaredsburrows/gradle-license-plugin"

  issueManagement {
    system "github"
    url "https://github.com/jaredsburrows/gradle-license-plugin/issues"
  }

  scm {
    url "https://github.com/jaredsburrows/gradle-license-plugin"
    connection "scm:https://github.com/jaredsburrows/gradle-license-plugin"
    developerConnection "scm:git@github.com:jaredsburrows/gradle-license-plugin.git"
  }

  licenses {
    license {
      name "The Apache Software License, Version 2.0"
      url "http://www.apache.org/licenses/LICENSE-2.0.txt"
      distribution "repo"
    }
  }

  developers {
    developer {
      id "jaredsburrows"
      name "Jared Burrows"
      email "jaredsburrows@gmail.com"
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = "sources"
  description = "Create jar of the sources of library."
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = "javadoc"
  description = "Create javadoc jar of Java documentation."
  from javadoc.destinationDir
}

task mainJar(type: Jar) {
  description = "Create compiled jar of library."
  from sourceSets.main.output
}

task testsJar(type: Jar) {
  classifier = "tests"
  description = "Create jar of all tests."
  from sourceSets.test.output
}

task reportsZip(type: Zip, dependsOn: check) {
  classifier = "reports"
  description = "Create a zip of all reports."
  from reporting.baseDir
}

publishing {
  publications {
    maven(MavenPublication) {
      artifact mainJar
      artifact sourcesJar
      artifact javadocJar
      artifact testsJar
      artifact reportsZip

      groupId project.group
      artifactId project.name
      version project.version

      pom {
        withXml {
          asNode().children().last() + pomConfig
        }
      }
    }
  }
}
publish.dependsOn jar, sourcesJar, javadocJar, testsJar, reportsZip
publish.dependsOn "generatePomFileForMavenPublication"

artifactory {
  contextUrl = "https://oss.jfrog.org"
  publish {
    repository {
      repoKey = "oss-snapshot-local"
      username = project.hasProperty("BINTRAY_USERNAME") ? project.property("BINTRAY_USERNAME") : System.getenv("BINTRAY_USERNAME")
      password = project.hasProperty("BINTRAY_API_KEY") ? project.property("BINTRAY_API_KEY") : System.getenv("BINTRAY_API_KEY")
    }
    defaults {
      publications "maven"
    }
  }
  resolve {
    repository {
      repoKey = "libs-release"
    }
  }
}
artifactoryPublish.dependsOn jar, sourcesJar, javadocJar, testsJar, reportsZip
artifactoryPublish.dependsOn "generatePomFileForMavenPublication"

bintray {
  user = project.hasProperty("BINTRAY_USERNAME") ? project.property("BINTRAY_USERNAME") : System.getenv("BINTRAY_USERNAME")
  key = project.hasProperty("BINTRAY_API_KEY") ? project.property("BINTRAY_API_KEY") : System.getenv("BINTRAY_API_KEY")
  publications = ["maven"]
  publish = true
  pkg {
    repo = "maven"
    name = project.name
    desc = project.description
    websiteUrl = "https://github.com/jaredsburrows/gradle-license-plugin"
    issueTrackerUrl = "https://github.com/jaredsburrows/gradle-license-plugin/issues"
    vcsUrl = "https://github.com/jaredsburrows/gradle-license-plugin.git"
    licenses = ["Apache-2.0"]
    labels = ["gradle", "license", "plugin"]
    githubRepo = "jaredsburrows/gradle-license-plugin"
    githubReleaseNotesFile = "README.md"
    version {
      name = project.version
      desc = project.description
      released = new Date()
      vcsTag = project.version
      mavenCentralSync {
        sync = false
        user = project.hasProperty("SONATYPE_USERNAME") ? project.property("SONATYPE_USERNAME") : System.getenv("SONATYPE_USERNAME")
        password = project.hasProperty("SONATYPE_PASSWORD") ? project.property("SONATYPE_PASSWORD") : System.getenv("SONATYPE_PASSWORD")
        close = "1"
      }
    }
  }
}
bintrayUpload.dependsOn jar, sourcesJar, javadocJar, testsJar, reportsZip
bintrayUpload.dependsOn "generatePomFileForMavenPublication"
